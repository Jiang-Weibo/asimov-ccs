!> @brief Vector unit tests.

module test_vector

  use MPI

  use pFUnit
  
  use accsvec, only : create_vector, axpy, norm
  use accs_kinds, only : accs_int, accs_real
  use accs_types, only : vector_init_data, vector
  use accs_utils, only : update

  implicit none

  integer(accs_int), parameter :: nrows = 10
  type(vector_init_data) :: vec_sizes=vector_init_data(nloc=-1, nglob=nrows, comm=MPI_COMM_NULL)

  private
  public :: MpiTestMethod
  public :: test_vector_zeros
  public :: test_vector_ones
  public :: test_axpy
  
contains

  @test(npes=[1,2,3,4])
  subroutine test_vector_zeros(this)
    
    class(MpiTestMethod), intent(inout) :: this

    class(vector), allocatable :: a
    real(accs_real) :: n

    vec_sizes%comm = this%getMpiCommunicator()
    call create_vector(vec_sizes, a)
    call update(a)
    
    n = norm(a, 2)
    @assertEqual(0.0, real(n))

    deallocate(a)
    
  end subroutine test_vector_zeros

  @test(npes=[1,2,3,4])
  subroutine test_vector_ones(this)
    
    class(MpiTestMethod), intent(inout) :: this

    class(vector), allocatable :: a
    real(accs_real) :: n
    
    vec_sizes%comm = this%getMpiCommunicator()
    call create_vector(vec_sizes, a)
    call set_vec_ones(a)

    n = norm(a, 2)
    @assertEqual(sqrt(real(nrows)), real(n))

    deallocate(a)
    
  end subroutine test_vector_ones

  @test(npes=[1,2,3,4])
  subroutine test_axpy(this)
    
    class(MpiTestMethod), intent(inout) :: this

    class(vector), allocatable :: a, b
    real(accs_real) :: n

    vec_sizes%comm = this%getMpiCommunicator()
    call create_vector(vec_sizes, a)
    call create_vector(vec_sizes, b)
    call set_vec_ones(a)
    call set_vec_ones(b)

    call axpy(-1.0_accs_real, a, b)

    n = norm(a, 2)
    @assertEqual(sqrt(real(nrows)), real(n))
    n = norm(b, 2)
    @assertEqual(0.0, n)
    
    deallocate(a)
    deallocate(b)
    
  end subroutine test_axpy

  ! @brief Helper subroutine to fill a vector with all ones.
  subroutine set_vec_ones(v)

    use accs_constants, only : insert_mode
    use accs_types, only : vector_values
    use accs_utils, only : set_values
    
    class(vector), allocatable :: v

    type(vector_values) :: val_dat
    integer(accs_int) :: i
    
    val_dat%mode = insert_mode
    allocate(val_dat%idx(nrows))
    allocate(val_dat%val(nrows))

    do i = 1, nrows
       val_dat%idx(i) = i - 1 ! Assuming PETSc Fortran interface is zero-indexed
    end do
    val_dat%val(:) = 1.0

    call set_values(val_dat, v)
    call update(v)

    deallocate(val_dat%idx)
    deallocate(val_dat%val)

  end subroutine set_vec_ones
  
end module test_vector
  
!!! Local Variables:
!!! mode: f90
!!! End:
